#compdef jupyter-o2
# A zsh autocompleter for jupyter.
_jupyter-o2() {
  o2_args=(
    '(-h,--help)'{-h,--help}'[Show this help message and exit.]'
    '(-u,--user)'{-u,--user}'[your O2 username]:USER:'
    '(--host)--host[host to connect to]:HOST:'
    '(-p,--port)'{-p,--port}'[available port on your system]:PORT:'
    '(-t,--time)'{-t,--time}'[maximum time for Jupyter session]:TIME:'
    '(-m,--mem)'{-m,--mem}'[memory to allocate for Jupyter]:MEM:(1G 4G 8G 16G 32G)'
    '(-c,-n)'{-c,-n}'[cores to allocate for Jupyter]:CORES:(1 2 4 8)'
    '(--partition)--partition[SLURM partition to use for Jupyter]:PARTITION:(interactive "gpu,gpu_quad --gres=gpu\:1")'
    '(-k,--keepalive)'{-k,--keepalive}'[keep interactive session alive after exiting Jupyter]'
    '(--kq,--keepxquartz)'{--kq,--keepxquartz}'[do not quit XQuartz]'
    '(--force-getpass)--force-getpass[use getpass instead of pinentry for password entry]'
    '(--no-browser)--no-browser[run without opening the browser]'
    '(-Y,--ForwardX11Trusted)'{-Y,--ForwardX11Trusted}'[enable trusted X11 forwarding, equivalent to ssh -Y]'
    '(-v,--verbose)'{-v,--verbose}'[increase verbosity level]'
    '(--version)--version[show the current version and exit]'
    '(--paths)--paths[show configuration paths and exit]'
    '(--generate-config)--generate-config[generate the default configuration file]'
  )
  _arguments "1: :_o2_cmds" ${o2_args[@]} && ret=0
}

_o2_cmds() {
  local commands; commands=(
    'notebook:Launch a Tornado based HTML Notebook Server.'
    'lab:Run a JupyterLab Server.'
  )
  _describe -t commands 'jupyter-o2 command' commands "$@"
}

_jupyter-o2 "$@"
# vim: ft=zsh sw=2 ts=2 et
